
configfile: "config/defaults.yaml"

rule all_sequence_counts:
    input:
        clade_counts = expand(
            "results/{virus}/seq_counts.tsv",
            virus=config["viruses"]
        )

"""
Summarize sequence counts from existing metadata for a particular virus
"""

rule subset_metadata:
    output:
        subset_metadata = "data/{virus}/subset_metadata.tsv.zst"
    params:
        s3_src = lambda w: config[w.virus]["s3_metadata"],
        subset_columns = lambda w: ",".join(config[w.virus]["subset_columns"]),
    shell:
        """
        aws s3 cp {params.s3_src:q} - \
            | zstd -c -d \
            | tsv-select -H -f {params.subset_columns:q} \
            | zstd -c > {output.subset_metadata}
        """

rule summarize_clade_sequence_counts:
    input:
        subset_metadata = "data/{virus}/subset_metadata.tsv.zst"
    output:
        clade_seq_counts = "results/{virus}/seq_counts.tsv"
    params:
        seq_count_options = lambda w: config[w.virus]["seq_count_options"]
    shell:
        """
        ./bin/summarize-clade-sequence-counts \
            --metadata {input.subset_metadata} \
            --output {output.clade_seq_counts} \
            {params.seq_count_options}
        """
